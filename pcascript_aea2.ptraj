
# Load topologys, each with a different              #
# name handler depicted inside []                   #
#####################################################
parm aea2_sol.prmtop [aeap]

#####################################################
# Load trajectories                   		#
# each with their respective topology.              #
#####################################################
trajin aea2_eq3.nc parm [aeap]
trajin aea2_eq3_2.nc parm [aeap]
trajin aea2_eq3_3.nc parm [aeap]
trajin aea2_eq3_4.nc parm [aeap]

#####################################################
# Move and translate the coordinates so they will   #
# fit as close as possible to the first frame.      #
# Only fit residues 1 through 77 (proteins)         #
# and ignore everything that matches the atom 'H'   #
# This means that no hydrogen atoms are going to be #
# part of the fitting.                              #
#####################################################
rms first :1-81&!@H=

#####################################################
# Create an average structure considering all       #
# the loaded frames and save it as a single frame   #
# using the AMBER restart format                    #
#####################################################
average crdset aea2-average

#####################################################
# CPPTRAJ works with datasets of multiple formats   #
# create a coordinate dataset that refers to the    #
# loaded frames. Call the loaded frames             #
# 'wdd-trajectories'                            #
#####################################################
createcrd aea2-trajectories

#####################################################
# Run the commands because we need our reference    #
# structure. With the 'run' command, the            #
# commands so far will run and will generate our    #
# average reference structure with the AMBER        #
# restart format                                    #
#####################################################
run

#####################################################
# Fit our frames, which we named:                   #
# wdd-trajectories                              #
# to the previously loaded average structure        #
# Always use the same mask                          #
#####################################################
crdaction aea2-trajectories rms ref aea2-average :1-81&!@H=

#####################################################
# Calculate coordinate covariance matrix            #
#####################################################
crdaction aea2-trajectories matrix covar \
name aea2-covar :1-81&!@H=

#####################################################
# Diagonalize coordinate covariance matrix          #
# Get first 3 eigenvectors                          #
#####################################################
runanalysis diagmatrix aea2-covar out aea2-evecs.dat \
vecs 3 name myEvecs \
nmwiz nmwizvecs 3 nmwizfile aea2_nmwiz.nmd nmwizmask :1-81&!@H= 

#####################################################
# Now create separate projections                   #
# for each set of trajectories                      #
# More details: pag 610                             #
#####################################################
crdaction aea2-trajectories projection AEAP modes myEvecs \
beg 1 end 3 :1-81&!@H= crdframes 1,48000

# histogram
hist AEAP:1 bins 100 out aea2-hist.agr norm name AEAP-1
hist AEAP:2 bins 100 out aea2-hist.agr norm name AEAP-2
hist AEAP:3 bins 100 out aea2-hist.agr norm name AEAP-3

run
clear all


#####################################################
## Visualize the fluctuations of the eigenmodes      #
## Read the file with the eigenvectores              #
######################################################
readdata aea2-evecs.dat name Evecs
#
#
######################################################
## Load a topology                                   #
## This is necesary to create a new topology         #
## that will match the read in eigenmodes
######################################################
parm aea2_sol.prmtop
parmstrip !(:1-81&!@H=)
parmwrite out aea2-modes.prmtop
#
######################################################
## Create a NetCDF trajectory file with the          #
## modes of motion of the first PCA                  #
######################################################
runanalysis modes name Evecs trajout aea2-mode1.nc \
pcmin -100 pcmax 100 tmode 1 trajoutmask :1-81&!@H= trajoutfmt netcdf

runanalysis modes name Evecs trajout aea2-mode2.nc \
pcmin -100 pcmax 100 tmode 2 trajoutmask :1-81&!@H= trajoutfmt netcdf

runanalysis modes name Evecs trajout aea2-mode3.nc \
pcmin -100 pcmax 100 tmode 3 trajoutmask :1-81&!@H= trajoutfmt netcdf
#
#
######################################################
## Now you can open the files:                       #
## wdd-modes.prmtop                              #
## wdd-modes.nc                                  #
## in Chimera / VMD and watch the movie              #
## which shows the first mode of motion              #
######################################################

